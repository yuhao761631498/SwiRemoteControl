apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

def rootProjectExt = rootProject.ext;
def jar_artifactId = 'gduUtil'
def jar_version = rootProjectExt.jar.version
def configs = rootProjectExt.configs;
def userName = configs.userName
def userPassword = configs.password
def mavenUrl = jar_version.contains('-SNAPSHOT') ? rootProjectExt.nexusUrl.nexusUrlSnapshot : rootProjectExt.nexusUrl.nexusUrlRelease


android {
    compileSdkVersion rootProjectExt.android.compileSdkVersion
    buildToolsVersion rootProjectExt.android.buildToolsVersion

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion rootProjectExt.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}


publishing {
    publications {
        gduUtil(MavenPublication) {
            groupId rootProjectExt.jar.groupId
            artifactId jar_artifactId
            version jar_version
            artifact "build/intermediates/bundles/release/classes.jar"

            //generate pom nodes for dependencies
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
            }
        }
    }

    //publish to filesystem repo
    repositories {
        maven {
            credentials {
                username userName
                password userPassword
            }
            url mavenUrl
        }
    }
}
configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
//    implementation 'com.android.support:support-compat:28.0.0'
    testImplementation 'junit:junit:4.12'
    implementation project(path: ':SWZNModel')
    implementation 'com.hynnet:jxl:2.6.12.1'
//    implementation 'com.android.support:support-v4:26.0.0-alpha1'
    api 'com.android.support:support-compat:26.+'
}
